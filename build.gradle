buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.yaml:snakeyaml:2.2")
    }
}

plugins {
    id("com.github.johnrengelman.shadow") version "7.1.1"
    id("java")
    id("application")
    id("org.graalvm.buildtools.native") version "0.9.28"
}

version = "0.1"
group = "jcli"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("info.picocli:picocli-codegen:4.6.3")
    implementation("info.picocli:picocli:4.6.3")
    implementation("org.yaml:snakeyaml:2.2")
    implementation("ch.qos.logback:logback-classic:1.2.10")
    
    // Test dependencies
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
}

application {
    mainClass.set("jcli.JcliCommand")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

graalvmNative {
    binaries {
        main {
            imageName.set("jcli") 
            buildArgs.add("--verbose")

        }
    }
}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

import org.yaml.snakeyaml.Yaml
import groovy.json.JsonBuilder

task generateReflectConfig {
    def commandsFile = file("src/main/resources/commands.yaml")
    def reflectConfigFile = file("src/main/resources/META-INF/native-image/jcli/jcli/reflect-config.json")
    
    inputs.file(commandsFile)
    outputs.file(reflectConfigFile)
    
    doLast {
        if (!commandsFile.exists()) {
             throw new GradleException("commands.yaml not found at ${commandsFile}")
        }
        
        def yaml = new Yaml()
        def data = yaml.load(commandsFile.newInputStream())
        def commands = data['commands']
        def reflectionEntries = []
        
        // Add CommandConfig for generic command loading
        reflectionEntries << [
            name: "jcli.config.CommandConfig",
            allDeclaredConstructors: true,
            allPublicConstructors: true,
            allDeclaredFields: true,
            allPublicMethods: true
        ]
        
        // Add command classes
        commands.each { cmd ->
            reflectionEntries << [
                name: cmd['className'],
                queryAllPublicMethods: true
            ]
        }
        
        reflectConfigFile.parentFile.mkdirs()
        reflectConfigFile.text = new JsonBuilder(reflectionEntries).toPrettyString()
        println "Generated reflect-config.json with ${reflectionEntries.size()} entries."
    }
}

processResources.dependsOn generateReflectConfig
